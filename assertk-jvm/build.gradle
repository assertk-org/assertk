buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath "me.tatarka.gradle:pom:1.0"
    }
}

apply plugin: 'java-library'
apply plugin: 'kotlin-platform-jvm'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    expectedBy project(":assertk-common")
    api "org.jetbrains.kotlin:kotlin-stdlib"
    api "org.jetbrains.kotlin:kotlin-reflect"
    implementation 'com.willowtreeapps.opentest4k:opentest4k-jvm:1.1.0'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    testImplementation "org.jetbrains.kotlin:kotlin-test"
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.61
            }
        }
    }
}

jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

test.finalizedBy(jacocoTestCoverageVerification)

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

apply plugin: 'org.jetbrains.dokka'
dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/docs"
}

apply from: '../publish.gradle'
