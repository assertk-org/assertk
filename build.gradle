allprojects {
    group 'com.willowtreeapps.assertk'
    version '0.12'
}

buildscript {
    ext.versions = [
        kotlin: '1.2.61',
        dokka:  '0.9.17',
        detekt: '1.0.0.RC8'
    ]

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "gradle.plugin.io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${versions.detekt}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath "me.tatarka.gradle:pom:1.0"
        classpath 'org.ajoberstar:gradle-git-publish:1.0.1'
    }
}

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"

    buildscript {
        repositories {
            jcenter()
            mavenCentral()
            maven { url "https://plugins.gradle.org/m2/" }
        }
    }

    repositories {
        jcenter()
        mavenCentral()
    }

    detekt {
        version = versions.detekt

        profile("main") {
            input = "$projectDir/src/main/kotlin"
            config = "$projectDir/../detekt.yml"
        }

        profile("test") {
            input = "$projectDir/src/test/kotlin"
            config = "$projectDir/../detekt-test.yml"
        }
    }
}

apply plugin: 'org.ajoberstar.git-publish'

gitPublish {
    repoUri = 'git@github.com:willowtreeapps/assertk.git'
    branch = 'gh-pages'
    contents {
        from 'assertk-jvm/build/docs'
        into 'javadoc'
    }
}
