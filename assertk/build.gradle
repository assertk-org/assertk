plugins {
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.kotlin.multiplatform'
    id 'org.jetbrains.dokka'
    id 'io.gitlab.arturbosch.detekt'
    id 'org.ajoberstar.git-publish'
}

apply from: "$rootProject.projectDir/multiplatform.gradle"

task compileTemplates(type: TemplateTask) {
    inputDir = file('src/template')
    outputDir = file("$buildDir/generated/template")
}

task compileTestTemplates(type: TemplateTask) {
    inputDir = file('src/testTemplate')
    outputDir = file("$buildDir/generated/testTemplate")
}

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation "com.willowtreeapps.opentest4k:opentest4k:$opentest4k_version"
            }
            kotlin.srcDirs += files(compileTemplates)
        }
        commonTest {
            kotlin.srcDirs += files(compileTestTemplates)
        }
        jvmMain {
            dependencies {
                compileOnly kotlin('reflect')
            }
        }
        jvmTest {
            dependencies {
                implementation project(':java-interop')
            }
        }
        jsMain {
            dependencies {
		// kotlin-js doesn't handle this as a transitive dep correctly
		// when it's scoped as runtime.
                api "com.willowtreeapps.opentest4k:opentest4k-js:$opentest4k_version"
            }
        }
    }
}

[compileKotlinJvm, compileKotlinJs].each { it.dependsOn(compileTemplates) }
[compileTestKotlinJvm, compileTestKotlinJs].each { it.dependsOn(compileTestTemplates) }

gitPublish {
    repoUri = 'git@github.com:willowtreeapps/assertk.git'
    branch = 'gh-pages'
    contents {
        from 'build/javadoc'
        into 'javadoc'
    }
}

gitPublishCopy.dependsOn(dokka)
