apply plugin: 'kotlin-platform-js'
apply plugin: 'com.moowork.node'

buildscript {
    dependencies {
        classpath "com.eriwen:gradle-js-plugin:2.14.1"
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}

dependencies {
    expectedBy project(":assertk-common")
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}


[compileKotlin2Js, compileTestKotlin2Js]*.configure {
    kotlinOptions.moduleKind = "umd"
    kotlinOptions.sourceMap = true
    kotlinOptions.sourceMapEmbedSources = "always"
}


task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
    from compileKotlin2Js.destinationDir

    configurations.testCompile.each {
        from zipTree(it.absolutePath).matching { include '*.js' }
    }

    into "${buildDir}/node_modules"
}

ext {
    nodeVersion = '8.9.4'
}

node {
    version = nodeVersion
    download = false
}

npmInstall.workingDir = file("..")

task runNodeTests(type: NodeTask, dependsOn: [compileTestKotlin2Js, npmInstall, populateNodeModules]) {
    script = file('../node_modules/.bin/mocha')
    args = [projectDir.toPath().relativize(file(compileTestKotlin2Js.outputFile).toPath())]
}

test.dependsOn runNodeTests
